apply plugin: 'com.android.application'

task processWithJavassist << {
    //项目编译class所在目录
    String classPath = file('build/intermediates/classes/debug')
    String hackBuildPach = project(':hackdex').buildDir.absolutePath + '/intermediates/classes/debug'
    String resourcePath = file('src/main/java/com/hotfix/demo')
    def file = file("../patch.properties")
    if (file) {
        com.groovy.BuildGroovy.process(resourcePath, classPath, hackBuildPach, file)
    }
}

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.hotfix"
        minSdkVersion Integer.parseInt(project.MINI_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.dex.dependsOn processWithJavassist //在执行dx命令之前将代码打入到class中
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile project(':hotfixlib')
}

def getSdkDir = {
    def file = file("../local.properties")
    Properties p = new Properties()
    file.withInputStream { stream ->
        p.load(stream)
    }
    return p.'sdk.dir'
}

task createHack(type: Exec) {
    workingDir getSdkDir() + '\\build-tools\\' + project.BUILD_TOOLS_VERSION
    commandLine 'cmd', '/c', 'dx --dex --output ' + project.rootDir.path + '/app/src/main/assets/hack_dex.jar ' + project.rootDir.path + '/hackdex/build/intermediates/bundles/debug/classes.jar'
}


task createPatchJar(type: Jar) {
    def file = file("../patch.properties")
    def classes = []
    if (file) {
        List<String> lines = file.readLines();
        for (line in lines) {
            if (line.startsWith('#')) {
                continue;
            }
            def className = line.replace('.', '/') + '.class'
            classes.add(className)
        }
    }
    baseName 'patch'
    from('build/intermediates/classes/debug/')
    include(classes)
}

task createPatchDex(type: Exec, dependsOn: createPatchJar)  {
    workingDir getSdkDir() + '\\build-tools\\' + project.BUILD_TOOLS_VERSION
    commandLine 'cmd', '/c', 'dx --dex --output ' + project.rootDir.path + '/app/src/main/assets/patch_dex.jar ' + project.rootDir.path + '/app/build/libs/patch.jar'
}

task createPatch(dependsOn: createPatchDex) << {
    def fileJar = file('build/libs/patch.jar')
    if (fileJar.exists()) {
        //fileJar.delete()
    }
}
